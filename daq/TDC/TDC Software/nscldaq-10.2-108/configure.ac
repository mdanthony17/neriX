#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.61)
AC_INIT(nscldaq, 10.2-108, fox@nscl.msu.edu)

AC_CONFIG_SRCDIR([/utilities/StringsToIntegers.h])
AC_CONFIG_HEADER([config.h])
AC_CONFIG_MACRO_DIR([m4])

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LIBTOOL


AC_CHECK_PROG(HAVE_DOCBOOK2PDF, [docbook2pdf], true, false)
AC_CHECK_PROG(HAVE_XMLTO, [xmlto], true, false)
AC_CHECK_PROG(MANDB, mandb, mandb, echo)

AC_CHECK_LIB([X11], [XSetWindowBackground])
AC_CHECK_LIB([Xt], [XtManage])

##
# Should move this to local tests...leaving here so branch merge with 11
# will remind me with a conflict rather than svn thinking it's ok
# to check/subst twice in different spots of this file.

AM_PATH_CPPUNIT

#
#   For compatibility with existing AC_Substs:

CPPUNIT_LDFLAGS="${CPPUNIT_LIBS}"



AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([arpa/inet.h fcntl.h limits.h malloc.h netdb.h netinet/in.h stdint.h stdlib.h string.h sys/socket.h sys/time.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.

AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_TYPE_UID_T
AC_TYPE_UINT16_T
AC_TYPE_UINT32_T
AC_TYPE_UINT8_T

# check to find the size of address
AC_CHECK_SIZEOF([void*])
AS_IF([test "$ac_cv_sizeof_voidp" -eq 8],
    AC_DEFINE([ADDR64],1,[Specifies that memory addresses are 64-bits ]),
     [])
   

# Checks for library functions.

AC_FUNC_ERROR_AT_LINE
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_CHECK_FUNCS([dup2 gethostbyaddr gethostbyname gettimeofday inet_ntoa memmove memset regcomp socket strchr strcspn strdup strerror strspn strtol])

AC_CHECK_FUNCS([ftruncate munmap])
AC_FUNC_MMAP
AC_FUNC_STAT


# Automake:

AM_INIT_AUTOMAKE([])

# Makefile exports -- need a way to compute this from the version string in the init macro
# Note that linker does something funky linke turn a:b:c into so.a.c.b

SOVERSION="10:2:0"





#---------------------------------------------------------------------
#
#  Local tests:
#

# Ensure swig is installed:-- this is used to generate wrappers for the
#  VMUSB and CCUSB libraries for use within Tcl drivers.
#
#  Earlier swig versions than 1.3 may well work, however this is the
#  version I used when first successfully building the wrappers
# (ubuntu lucid lynx).
#
AX_PKG_SWIG([1.3], [], [ AC_MSG_ERROR([Swig is required to build nscldaq])])


# We use gengetopt in a few places.  Ideally it's been found by now in the
# path if not we'll need to do the withval thing.. or prompt the user to
# give us a --with-gengetopt-path
#

AX_GENGETOPT


# Tcl/Tk is pervasive in NSCLDAQ so we need it too:

AX_TCL

#
#  The substs below are for compatibility sake 
#  eventually we should migrate Makefile.am defs to use
#  those in m4/tcl.m4
#
TCL_FLAGS="${TCL_CPPFLAGS} ${TK_CPPFLAGS}"
TCL_LDFLAGS="${TK_LIBS} ${TCL_LIBS}"
TCLSH_CMD=${TCLSH} 
AC_SUBST(TCL_FLAGS)
AC_SUBST(TCL_LDFLAGS)
AC_SUBST(TCLSH_CMD)



# Epics:

#
#  This is only probed for and build if --enable-epics-tools=yes is provided
#  at configuration time.  Oncethat's done, AX_EPICS provideas all the defs
#  and the EPICS/BUILD_EPICS_TOOLS enables those in the Makefiles.
#


BUILD_EPICS_TOOLS=no
AC_ARG_ENABLE([epics-tools],
	AS_HELP_STRING([--enable-epics-tools=yes/no], 
	[Build software that requires EPICS (default no)]),
	[BUILD_EPICS_TOOLS="yes"])

if test "$BUILD_EPICS_TOOLS" == "yes"
then

	AX_EPICS
	AM_CONDITIONAL([EPICS], [true])
	AM_CONDITIONAL(BUILD_EPICS_TOOLS, [true])

else
	AM_CONDITIONAL([EPICS], [false])
	AM_CONDITIONAL(BUILD_EPICS_TOOLS, [false])

fi



#
#  The SBS software will only be built if
#  --enable-sbs=kernel-source-dir was supplied.
#  In that case, the configured kernel source is assumed to be in 
#  kernel-source-dir and that will be used to compile the device driver
#  as well.  
#  The default is to _not_ build the sbs stuff.
#

BUILD_SBS="no"

AC_ARG_ENABLE([sbs], 
	      AS_HELP_STRING([--enable-sbs=kernel-source-dir], [Build SBS software using kernel-source-dir as directory with configured kernel source]),
	      [BUILD_SBS="yes"
	       KERNEL_SOURCE_DIR="$enableval"])




if test "$BUILD_SBS" == "yes"
then
  echo Building SBS software with kernel source at: $KERNEL_SOURCE_DIR
else
  echo SBS software support not selected!
fi


#
#  If --enable-usb is turned on, we build the VM/CC-usb support directories
#
enableusb="no"
AC_ARG_ENABLE([usb],
	AS_HELP_STRING([--enable-usb], [Build support for CC/VM usb data taking]),
	[enableusb="yes"])

#  If building usb is turned on we need to locate the headers and the includes.


enableusb="no"
AC_ARG_ENABLE([usb],
	AS_HELP_STRING([--enable-usb], [Build support for CC/VM usb data taking]),
	[enableusb="yes"])

#  If building is turned on we need to locate the headers and the includes.


if test "$enableusb" == "yes"
then
	AX_LIBUSB
	AM_CONDITIONAL([BUILD_USB], [true])
else
	AM_CONDITIONAL([BUILD_USB], [false])
fi

#
#  IF systemtcl is yes we're pretty much done.. otherwise
#  we need to configure/build/install libtcl into prefix and then set
#  the flags:

AC_MSG_CHECKING([libtcl++ installed as package (e.g. .deb)])

AC_CHECK_FILE([/usr/include/TCLInterpreterObject.h], [systemtcl="yes"], [systemtcl="no"])

if test $systemtcl = "yes"
then
	LIBTCLPLUS_CFLAGS=""
	LIBEXCEPTION_LDFLAGS="-lException"
	LIBTCLPLUS_LDFLAGS="-ltclPlus ${LIBEXCEPTION_LDFLAGS}"
	AC_MSG_RESULT([Found in  system libraries])
else
	AC_MSG_RESULT([Need to build local tagged copy])
	AC_CHECK_FILE([./libtcl/configure], [havelibtcldir="yes"], [havelibtcldir="no"])
	if test $havelibtcldir = "yes"
	then
		(cd libtcl; ./configure --prefix=${prefix}; make clean install)
		LIBTCLPLUS_CFLAGS="-I${prefix}/include"
		LIBEXCEPTION_LDFLAGS="-L${prefix}/lib -lException -Wl,\"-rpath=${prefis}/lib\""
		LIBTCLPLUS_LDFLAGS="-L${prefix}/lib -ltclPlus ${LIBEXCEPTION_LDFLAGS}"
	else
		AC_MSG_ERROR([I seem to be missing libtcl, please use tcl++incorp.sh to fill it with a tagged release of libtclplus])
	fi

fi
AC_SUBST(LIBTCLPLUS_CFLAGS)
AC_SUBST(LIBTCLPLUS_LDFLAGS)
AC_SUBST(LIBEXCEPTION_LDFLAGS)


#----------------------------------------------------------------------------

# Exports to the makefiles:

AC_SUBST(SOVERSION)
AC_SUBST(MANDB)



AC_SUBST(CPPUNIT_LDFLAGS)

AM_CONDITIONAL([PDFDOCS], [test x$HAVE_DOCBOOK2PDF = xtrue])
AM_CONDITIONAL([XMLMANS], [test x$HAVE_XMLTO = xtrue])

#
#  Threading is hard coded to -pthread now.
#
THREADCXX_FLAGS="-pthread"
THREADC_FLAGS="-pthread"
THREADLD_FLAGS="-lpthread -lrt"

AC_SUBST(THREADCXX_FLAGS)
AC_SUBST(THREADC_FLAGS)
AC_SUBST(THREADLD_FLAGS)

# SBS stuff:

AC_SUBST(KERNEL_SOURCE_DIR)
AM_CONDITIONAL([BUILD_SBS_DRIVER], [test "$BUILD_SBS" == "yes"])




#---------------------------------------------------------------------------
# Generate the following from their .in's (note that Automake takes
# *.am -> *.in.
#

AC_CONFIG_FILES([Makefile
		nscldaq
		base/Makefile
		base/os/Makefile
                 base/CopyrightTools/Makefile
                 base/cvt/Makefile
		 base/thread/Makefile
                 base/headers/Makefile
                 base/security/Makefile
                 base/tclwidgets/Makefile
                 base/uri/Makefile
		 base/dataflow/Makefile
		 base/tcpip/Makefile
		 servers/Makefile
		 servers/tclserver/Makefile
                 servers/portmanager/Makefile
		 utilities/Makefile
                 utilities/daqstart/Makefile
                 utilities/dvdburn/Makefile
                 utilities/scalerdisplay/Makefile 
		 utilities/scalerdisplay/ScalerDisplay
                 utilities/sequencer/Makefile
		 utilities/ringselector/Makefile
		 utilities/bufdump/Makefile
		 utilities/eventlog/Makefile
		 utilities/sclclient/Makefile
		 utilities/tkbufdump/Makefile
		 utilities/compatibility/Makefile
		 utilities/compatibility/eventlog-compat
		 utilities/compatibility/spectcldaq
		 utilities/compatibility/s800toring
                 epics/chanlog/Makefile
                 epics/controlpush/Makefile
                 epics/epicsdisplay/Makefile
		 epics/epicsdisplay/epicsdisplay
		 epics/Makefile
		 daq/Makefile
		 daq/format/Makefile
		 daq/readoutgui/Makefile
		 daq/readoutgui/InstallRoot.tcl
		 daq/s800callouts/Makefile
		 daq/eventbuilder/Makefile daq/eventbuilder/Makefile_Skeleton
		 daq/eventbuilder/ringsource/Makefile
		 daq/eventbuilder/teering/Makefile
		 daq/eventbuilder/glom/Makefile
		 daq/eventbuilder/unglom/Makefile
		 daq/eventbuilder/startOrderer
		 sbs/Makefile
		 sbs/driver/Makefile
		 sbs/driver/src/Makefile
		 sbs/driver/include/Makefile
		 sbs/driver/dd/GNUmakefile
		 sbs/nsclapi/Makefile
		 sbs/tclpackage/Makefile
		 sbs/puretcl/Makefile
		 sbs/vmemodules/Makefile
		 sbs/readout/Makefile
		 sbs/readout/UserMakefile
		 sbs/readout/SBSRdoMakeIncludes
		 sbs/slowcontrols/Makefile
		 sbs/slowcontrols/vhq/Makefile
		 sbs/slowcontrols/caenv812/Makefile
		 sbs/slowcontrols/n568b/Makefile
		 sbs/slowcontrols/bcnaf/Makefile
		 sbs/slowcontrols/vhs/Makefile
		 sbs/scripted/Makefile
		 sbs/VmeDevice/Makefile
		 usb/Makefile
		 usb/vmusb/Makefile
		 usb/vmusb/vmusb/Makefile usb/vmusb/devices/Makefile 
		 usb/vmusb/threadcomm/Makefile usb/vmusb/router/Makefile
		 usb/vmusb/rdothread/Makefile usb/vmusb/tclserver/Makefile 
		 usb/vmusb/daqconfig/Makefile  usb/vmusb/commands/Makefile
		 usb/vmusb/driverkit/Makefile usb/vmusb/driverkit/Makefile-template
		 usb/vmusb/tcldrivers/Makefile
		 usb/vmusb/driverkit/VMUSBDriverIncludes
		 usb/ccusb/Makefile
		 usb/ccusb/ccusb/Makefile usb/ccusb/devices/Makefile 
		 usb/ccusb/threadcomm/Makefile usb/ccusb/router/Makefile
		 usb/ccusb/rdothread/Makefile usb/ccusb/tclserver/Makefile 
		 usb/ccusb/daqconfig/Makefile  usb/ccusb/commands/Makefile
		 usb/ccusb/driverkit/Makefile
		 usb/ccusb/driverkit/Makefile-template
		 usb/ccusb/driverkit/CCUSBDriverIncludes
		 docbuild/Makefile
		 docconfig/Makefile])

AC_OUTPUT

