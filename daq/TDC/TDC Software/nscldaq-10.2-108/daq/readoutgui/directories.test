#  Test the ExpFileSystem package

package require tcltest

# Setup the tests by ensuring that we have a package
# directory for the script dir and that it is in the
# auto_path.

set here [file dirname [info script]]
set oldPath $auto_path

lappend auto_path $here

if {[file exists [file join $here pkgIndex.tcl]]} {
    file delete pkgIndex.tcl.saved
    file rename [file join $here pkgIndex.tcl] \
                [file join $here pkgIndex.tcl.saved]
}
pkg_mkIndex $here *.tcl

#  Set up the configuration of the directory system:


package require ExpFileSystemConfig
package require ExpFileSystem

# We'll set up a staging area and an expdir below us:

set stagearea [::tcltest::makeDirectory stagearea]
set expdir    [file join stagearea .. expdir]

set env(EVENTS) $stagearea
set env(EXPDIR) $expdir
catch {unset env(BUFFERSIZE)}

ExpFileSystemConfig::setDefaults
ExpFileSystemConfig::environmentOverrides
DAQParameters::setDefaults


# Utilities:

proc isDirectory {path} {
    if {![file exists $path]} {
        return 0
    }
    if {![file isdirectory $path]} {
        return 0
    }
    return 1
}

# Test ability to create correct root names.

tcltest::test ExpFileSystem-1.0 {ExpFileSystem::GetStage} {
    ExpFileSystem::GetStage
} "$stagearea"

# WhereisCurrentData:

tcltest::test ExpFileSystem-2.0 {ExpFileSystem::WhereisCurrentData} {
    ExpFileSystem::WhereisCurrentData
} [file join $stagearea experiment current]

# WhereisRun:

tcltest::test ExpFileSystem-3.0 {ExpFileSystem::WhereisRun}  {
    ExpFileSystem::WhereisRun 5
} [file join $stagearea experiment run5]

#  Tests to figure out the name of a run event file:

tcltest::test ExpFileSystem-4.0 {ExpFileSystem::GenRunFileBase} {
    ExpFileSystem::GenRunFileBase 123
} run123

tcltest::test ExpFileSystem-4.1 {ExpFileSystem::GenRunFile seq = 0} {
    ExpFileSystem::GenRunFile 432
} run432-4096.evt

tcltest::test ExpFileSystem-4.2 {ExpFileSystem::GenRunFile seq = 456} {
    ExpFileSystem::GenRunFile 432 456
} run432_456-4096.evt

tcltest::test ExpFileSystem-4.3 {ExpFileSystem::WhereisRunFile 555} {
    ExpFileSystem::WhereisRunFile 555
} [file join $stagearea experiment run555 run555-4096.evt]

tcltest::test ExpFileSystem-4.4 {ExpFileSystem::WhereisRunFile 555 666} {
    ExpFileSystem::WhereisRunFile 555 666
} [file join $stagearea experiment run555 run555_666-4096.evt]

#  Stagearea pieces:

tcltest::test ExpFileSystem-5.0 {ExpFileSystem::WhereisCurrentEventData} {
    ExpFileSystem::WhereisCurrentEventData
} [file join $stagearea current]

tcltest::test ExpFileSystem-5.1 {ExpFileSystem::WhereareCompleteEventAreas} {
    ExpFileSystem::WhereareCompleteEventFiles
} [file join $stagearea complete]

tcltest::test ExpFileSystem-5.2 {ExpFileSystem::WhereareRetainedEventFiles} {
    ExpFileSystem::WhereareRetainedEventFiles
} [file join $stagearea retained]

tcltest::test ExpFileSystem-5.3 {ExpFileSystem::WhereisStagedMetaData} {
    ExpFileSystem::WhereisStagedMetaData

} [file join $stagearea staged]

#  Check that the directories can be created correctly.

tcltest::test ExpFileSystem-6.0 {ExpFileSystem::CreateHierarchy} {
    global stagearea
    global expdir
    ExpFileSystem::CreateHierarchy

    set result 1
    if {![isDirectory [file join $stagearea current]]} {
        set result "no stagearea/current dir"
    }

    if {![isDirectory [file join $stagearea complete]]} {
        set result "no stagearea/complete dir"
    }
    if {![isDirectory [file join $stagearea experiment current]]} {
        set result {no stagearea/experiment/current dir}
    }
    if {![isDirectory [file join $stagearea retained]]} {
        set result {no stagearea/retained dir.}
    }
    set link [file readlink $expdir]
    set dir  [file join $stagearea experiment]
    if {$link != $dir} {
        set result [list link: $link  sb: $dir]
    }

    set result

} 1

#   Determine where the metadata base dir is:

tcltest::test ExpFileSystem-7.0 {ExpFileSystem::WhereisMetadata} {
    ExpFileSystem::WhereisMetadata
} [file join $stagearea experiment]

#Undo our changes to the auto_path and the package index.

set auto_path $oldPath

file delete pkgIndex.tcl

if {[file exists [file join $here pkgIndex.tcl.saved]]} {
    file rename [file join $here pkgIndex.tcl.saved] \
                [file join $here pkgIndex.tcl]
}
#  Get rid of the directories we made:

::tcltest::removeDirectory $stagearea
file delete $expdir

