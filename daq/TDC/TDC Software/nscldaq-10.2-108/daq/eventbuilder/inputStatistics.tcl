#    This software is Copyright by the Board of Trustees of Michigan
#    State University (c) Copyright 2005.
#
#    You may use this software under the terms of the GNU public license
#    (GPL).  The terms of this license are described at:
#
#     http://www.gnu.org/licenses/gpl.txt
#
#    Author:
#             Ron Fox
#	     NSCL
#	     Michigan State University
#	     East Lansing, MI 48824-1321

##
# @file inputStatistics.tcl
# @brief widget collection for displaying input queue statistics.
#

#  Add here to the package include path if it's not there already:

set here [file dirname [info script]]
if {[lsearch $::auto_path $here] == -1} {
    lappend ::auto_path $here
}

package require Tk
package require snit
package require EVBUtilities
package provide EVB::inputStatistics 1.0



#
# Define ::EVB::inputStatistics namespace.
#
namespace eval EVB {
    namespace eval inputStatistics {}
    namespace eval test {
        namespace eval inputStatistics {}
    }
}

##
# @file inputStatistics.tcl
# @author Ron Fox
# @brief  Input file statistics display widgets.
#
# This file defines  megawidgets that can be used to display the
# event builder input stage statistics.  The package defines
# three widgets:
#   EVB::inputStatistics::summaryDisplay - summarize the data.
#   EVB::inputStatistics::queueDisplay   - Displays the data from one queue.
#   EVB::inputStatistics::statusDisplay  - Glues the previous widgets together
#                                          into a single page.
#------------------------------------------------------------------------------

##
# @class EVB::inputStatistics::summaryDisplay
#
# This snit::widget provides a display of the statistics
# summary data.  Specifically:
#    - The oldest timestamp in flight
#    - The newest timestamp in flight.
#    - The The number of input sources that have received data.
#    
#   These are laid out as follows:
#
# \verbatim
#   +-----------------------------------------------------+
#   | Total number of queued fragments: <fragmentcount>   |
#   | Oldest timestamp: <stamp>  Newest timestamp <stamp> |
#   | Deepest queue              id : depth               |
#   +-----------------------------------------------------+
#
# \endverbatim
#
# OPTIONS:
#    - -fragments  Provides the total number of fragments.
#    - -oldest     Provides the oldest timestamp.
#    - -newest     Provides the newest timestamp.
#    - -deepestid  ID of deepest queue.
#    - -deepestdepth Fragments in deepest queue.
#
# @note that all other options are delegated to the frame.
# @note all methods are also delegated to the frame.
#
snit::widget ::EVB::inputStatistics::summaryDisplay {
    component innerHull
    
    option -fragments  -default 0
    option -oldest     -default 0
    option -newest     -default 0
    option -deepestid  -default ""
    option -deepestdepth -default ""
    
    
    delegate option * to innerHull
    delegate method * to innerHull
    
    constructor args {

        
        install innerHull using ttk::labelframe $win.innerhull
        
        #
        #  Define the widgets:
        #
        ttk::label $innerHull.fraglabel -text "Queued Fragments: "
        ttk::label $innerHull.oldlabel  -text "Oldest Timestamp: "
        ttk::label $innerHull.newlabel  -text "Newest Timestamp: "
        ttk::label $innerHull.deepIdLabel -text "Deepest Queue (id : depth)"
        ttk::label $innerHull.deepIdSepLabel -text " : "
	

        
        ttk::label $innerHull.fragments -textvariable ${selfns}::options(-fragments)
        ttk::label $innerHull.oldest    -textvariable ${selfns}::options(-oldest)
        ttk::label $innerHull.newest    -textvariable ${selfns}::options(-newest)
        ttk::label $innerHull.deepid    -textvariable ${selfns}::options(-deepestid)
        ttk::label $innerHull.deepdepth -textvariable ${selfns}::options(-deepestdepth)
	
        
        # Layout the widgets
        #
        grid $innerHull.fraglabel          $innerHull.fragments
        grid $innerHull.oldlabel           $innerHull.oldest 
        grid $innerHull.newlabel           $innerHull.newest

        grid $innerHull.deepIdLabel    -row 3 -column 0
        grid $innerHull.deepid         -row 3 -column 1 -sticky e
        grid $innerHull.deepIdSepLabel -row 3 -column 2
        grid $innerHull.deepdepth      -row 3 -column 3

        grid $innerHull -sticky nsew
        
        $self configurelist $args
    }
}


##
# @class EVB::inputStatistics::queueDisplay
#
#    Create a display of the statistics for a single queue:
#
# OPTIONS:
#   - -id        - Queue source identifier.
#   - -depth     - Queue depth
#   - -oldest    - Timestamp at frontof queue.
#   - -outcount  - Count of output fragments from this queue.
#
# DELEGATIONS:
#   - all other options -> hull
#   - all methods -> hull.
# LAYOUT:
#  \verbatim
#
#   +------------------------------------------------+
#   |               Source ID: <id>                  |
#   |  Depth      <depth>   Oldest: <timestamp>      |
#   |  Out frags: <n-output>
#   +------------------------------------------------+
#
#  \endverbatim
#
snit::widget ::EVB::inputStatistics::queueDisplay {

    
    option -id       -default -1
    option -depth    -default  0
    option -oldest   -default  0
    option -outcount -default  0
    
    delegate option * to innerHull
    delegate method * to innerHull
    
    ##
    # constructor
    #
    #   process the args, build the UI.
    #
    # @param args - configuration option key/value pairs
    #
    constructor args {

        
        install innerHull using ttk::frame $win.hull
        set innerHull $win.hull
        
        # construct the UI elements.
        
        ttk::label $innerHull.idlabel    -text {Source ID: }
        ttk::label $innerHull.depthlabel -text {depth:     }
        ttk::label $innerHull.oldlabel   -text {Oldest:    }
        ttk::label $innerHull.outcountlbl -text {Out frags:}
       
        ttk::label $innerHull.id     -textvariable ${selfns}::options(-id)
        ttk::label $innerHull.depth  -textvariable ${selfns}::options(-depth)
        ttk::label $innerHull.oldest -textvariable ${selfns}::options(-oldest)
        ttk::label $innerHull.outcount -textvariable ${selfns}::options(-outcount)
        
        $self configurelist $args
        
        # Lay them out.
        
        grid  x                    $innerHull.idlabel     $innerHull.id
        grid $innerHull.depthlabel $innerHull.depth       $innerHull.oldlabel   $innerHull.oldest
        grid $innerHull.outcountlbl $innerHull.outcount
        
        grid $innerHull -sticky nsew
    }
}

##
# @class EVB::inputStatistics::queueStats 
#
#  Widget to show all the per queue statistics.  This is a sorted widget
#  where each widget associated with a data source is a queueDisplay widget.
#
# METHODS:
#   updateQueue source depth oldest output-count
#       Updates the widget for the specified data source (creating a new one
#       as needed).
#   clear - Sets all counters in all displayed data source widgets to zero.
#   reset - Destroys all of the widgets.
#
snit::widgetadaptor EVB::inputStatistics::queueStats  {
    
    delegate method reset to hull
    delegate option *     to hull
    
    ##
    # constructor
    #
    #  installs the sorted widget as a hull with the appropriate connections
    #  to support the creation and updating of the child widgets:
    #
    constructor args {
        installhull using EVB::utility::sortedWidget \
            -lefttitle src -create [mymethod _CreateWidget] \
            -update [mymethod _UpdateWidget] \
            -width 250 -height 300
        
        $self configurelist $args
    }
    #-------------------------------------------------------------------------
    #
    #  Public methods.
    
    ##
    # updateQueue source depth oldest output-count
    #
    #  Update the counters etc. in a queue element widget:
    #
    # @param source - The id of the data source queue.
    # @param depth  - The current number of elements in the queue.
    # @param oldest - The timestamp of the oldest element in the queue (head)
    # @param outfrags - The number of fragments that have been removed from the
    #                   queue for output.
    #
    method updateQueue {source depth oldest outfrags} {
        $hull update $source [list $source $depth $oldest $outfrags]
    }
    ##
    # clear
    #
    #    Clear the counters for all elements.  Specifically
    #    - depth - set to zero.
    #    - oldest - set to zero.
    #    - outfrags - set to zero.
    #
    #
    method clear {} {
        set idList [$hull listids]
        foreach id $idList {
            $self update $id 0 0 0 0
        }
    }
    
    #--------------------------------------------------------------------------
    #  Private methods
    #
    
    ##
    # _CreateWidget widget
    #
    #   Create a queueDisplay widget with the specified window path.
    #
    # @param widget - Widget path to use when creating this widget.
    #
    method _CreateWidget widget {
        EVB::inputStatistics::queueDisplay $widget
    }
    ##
    # _UpdateWidget data
    #
    #   Called to update the information held by one of the sorted widgets.
    #
    # @param widget - path to the widget to modify.
    # @param data   - List that contains the data to put in the widget.
    #                 Elements in the list are, in order:
    #                 - source : The event source id.
    #                 - depth : The number of elements in the queue.
    #                 - oldest : The timestamp of the oldest queue element.
    #                 - output-count : The number of fragments that have been
    #                   output from this queue.
    #
    method _UpdateWidget {widget data} {
        $widget configure \
            -id     [lindex $data 0] -depth    [lindex $data 1] \
            -oldest [lindex $data 2] -outcount [lindex $data 3]
    }
}

##
# @class EVB::inputStatistics::statusDisplay
#
#  This widget builds a full status display from a summary and zero or
#  more queue displays.  It is up to the caller to add and remove widgets
#  The widgets themselves are organized in a grid with  -columns across.
#  of queue displays with the summary centered over the top of them.
#  Each queue display is identified by its queue id.  Methods allow you to
#  - add a new queue element
#  - remove an existing queue element
#  - list the queue element displays that are already present.
#
#
#  OPTIONS:
#    - -fragments - current number of in flight fragments.
#    - -oldest    - Oldest timestamp.
#    - -newest    - Newest timestamp.
#    - -deepestid - Id of queue with deepest id.
#    - -deepestdepth - Depth of deepest queue.
#    - -columns   - Columns in the grid of queue status info.
#
# METHODS:
#   - addDataSource    - adds a new data source, returns the widget.
#   - removeDataSource - Removes  data source from the grid..
#   - updateDataSource - Updates a data source display configuration
#   - getDataSource    - Gets the widget associated with  a data source.
#   - listSources      - Lists the ids of all displayed data sources.
#
# @note Data sources are laid out left to right top to bottom in
#       increasing order of their source ids.
#
snit::widget EVB::inputStatistics::statusDisplay {
    component summary;            # Summary widget.
    component queues;             # Frame containing individual queue statuses.
    
    option -columns  -default 1 -configuremethod configColumns
    
    delegate option -fragments to summary
    delegate option -oldest    to summary
    delegate option -newest    to summary
    delegate option -deepestid to summary
    delegate option -deepestdepth to summary
    
    delegate option * to queues
    
    variable sourceWidgets -array [list]

    #-----------------------------------------------------------------------
    #
    # Canonicals:
    
    ##
    # constructor
    #    Builds two frames.  The first frame contains a summary widget,
    #    the second frame is blank for now.  The bottom one will be used
    #    to grid in queue status objects as they are added.
    #
    # @param args - Option value pairs to configure the wdidges and us.
    #
    constructor args {
        #
        # Construct the components.
        #
        ttk::frame $win.summaryFrame
        install summary using ::EVB::inputStatistics::summaryDisplay \
            $win.summaryFrame.summary
        set summary $win.summaryFrame.summary
        
        install qeueues using ttk::frame $win.queueFrame -relief ridge -borderwidth 2
        set queues $win.queueFrame
        

        
        # Configure the component.
        
        $self configurelist $args;     # Configure us and the components
        
        # Layout the UI components.
        
        grid $win.summaryFrame.summary -sticky ew
        grid $win.summaryFrame         -sticky new
        grid $win.queueFrame           -sticky nsew
    }

    #------------------------------------------------------------------------
    #
    #  Option processing methods.
    
    ##
    #  configColumns
    #
    #   Called when the number of columns is changing.
    #   - Check the column is a legal integer parameter.
    #   - Check the column is >0
    #   - update options(-columns)
    #   - relayout the grid of data.
    #
    # @param option - Name of option being modified (-columns).
    # @param value  - Proposed new value.
    #
    method configColumns {option value} {
        if {![string is integer -strict $value]} {
            error "$option must be an integer"
        }
        if {$value <= 0} {
            error "$option must have at least one column"
        }
        
        set options($option) $value
        
        $self _LayoutQueueStatusGrid
    }
    #------------------------------------------------------------------------
    #  Public methods:
    #
    
    ##
    # addDataSource
    #
    #    Adds a new data source to the set of data source widgets displayed in
    #    the queues frame.
    #
    # @param id - Id of the source.
    # @param args - option/value list for the widget creation.
    #
    method addDataSource {id args} {
        
        # must be a new data source:
        
        if {$id in [array names sourceWidgets]} {
            error "Data source $id already has a widget on display"
        }
        # Create the new widget.  The new widget name will be
        # $win.queues.source$id .
        
        set newWidget [::EVB::inputStatistics::queueDisplay $queues.source$id \
                       -id $id -relief groove -borderwidth 2 {*}$args]
        
        #  figure out where it goes.  It's ranking is how many sources
        #  have smaller ids.
        #
        
        set ranking 0
        set sources [lsort -integer [array names sourceWidgets]];  # Sorted sources.
        #
        #  When this loop is done, sources only has the indices of higher rank.
        #  furthemore ranking says how many are in front of us.
        foreach sourceId $sources {
            if {$sourceId > $id} {
                break
            }
            incr ranking
            set sources [lrange $sources 1 end]
        }
        
        
        #  Tell the grid to forget the sources at higher rank than us:
        
        $self _forgetSources $sources
        
        # Add us to the widget array and prepend us tothe sources list:

        set sourceWidgets($id) $newWidget
        set sources [concat $id $sources]
        $self _regridSources $ranking $sources       
        
    }
    
    ##
    # removeDataSource
    #
    #  Remove an existing data source from plot.  All widgets after it in the
    #  gridding order get moved up to take its spot.
    #
    # @param id - Source id to remove.
    #
    method removeDataSource id {
        # Ensure the source is in the widget set:
        
        set sourceList [lsort -integer [array names sourceWidgets]]
        if {$id ni $sourceList} {
            error "Data source $id does not have a widget to remove."
        }
        # figure out the rank of this widget and remove
        # all the widgets that won't be moving from the front of the  list.
        #
        set rank [lsearch -exact $sourceList $id]
        set removeEnd [expr {$rank -1}];    # Index of last guy to remove..
        #
        #  If we are the first item nothing is removed:
        #
        if {$removeEnd >= 0} {
            set sourceList [lrange $sourceList 0 $removeEnd]
        }
        #  Forget the widgets remaining in sourceList, destroy our widget
        #  and remove it from the list too:
        
        $self _forgetSources $sourceList

        set sourceList [lrange 1 end]
        destroy $sourceWidgets($id)
        unset sourceWidgets($id)
        
        $self _regridSources $ranking $sourceList         
        
    }
    
    ##
    # updateDataSource
    #
    #  Updates the values in a data source.  The parameters are actually
    #  just passed to a reconfig of the widget.
    #
    # @param id   - Id of the data source.
    # @param args - Configuration option pairs.
    #
    method  updateDataSource {id args} {
        # Ensure this is a known source:
        
        if {$id ni [array names sourceWidgets]} {
            error "$id is not a data source we know anythingn about"
        }
        $sourceWidgets($id) configure {*}$args
    }
    
    ##
    # getDataSource
    #
    #  Return the widget associated with a data source.  This allows you to
    #  directly manipulate the apperance of that widget.  Do not destroy it under
    #  penalty of likely irrational behavior.
    #
    # @param id - source id.
    #
    method getDataSource id {
        # Ensure this is a known source:
        
        if {$id ni [array names sourceWidgets]} {
            error "$id is not a data source we know anythingn about"
        }
        return $sourceWidgets($id)        
    }
    
    ##
    # listSources
    #  List the data sources that are known to this 
    #  Widget manager.
    #
    #  @return list of integers.
    #
    method listSources {} {
        return [array names sourceWidgets]
    }
    #-------------------------------------------------------------------------
    #
    # Private methods (all start with _)
    
    
    ##
    # _regridSources
    # Regrid a list of widgets.
    #
    # @param ranking - where they belong in ranking
    # @param sourceListist - List of source Ids.
    #
    method _regridSources {ranking sourceList} {
        # now we just need to figure out where to start re-gridding widgets:
        
        set row [expr {int($ranking/$options(-columns))}]
        set column [expr {$ranking % $options(-columns)}]
       
        # And re-grid them:
        
        foreach source $sourceList {
            grid $sourceWidgets($source) -row $row -column $column
            incr column
            if {$column >= $options(-columns)} {
                incr row
                set column 0
            }
        }        
    }
    ##
    # _forgetSources
    # Forget the widgets that correspond to a set of sources:
    #
    # @param sourceList - list of data sources.
    #
    method _forgetSources sourceList {
        foreach source $sourceList {
            grid forget $sourceWidgets($source)
        }    
    }
    ##
    # _LayoutQueueStatusGrid
    #
    # Normally called when the grid configuration changtes.
    # forget all the sources and relayout:
    #
    method _LayoutQueueStatusGrid {} {
        set sourceList [lsort -integer [array names  sourceWidgets]]
        $self _forgetSources $sourceList
        $self _regridSources 0 $sourceList
    }
}

#-----------------------------------------------------------------------------
#
#  Integral test procs/methods:

##
#  Demo for EVB::inputStatistics::summaryDisplay.
#  Brings up a top level with the summary display in side and a control panel
#  that allows you to manipulate the display.
#

proc ::EVB::test::inputStatistics::summaryDisplay {} {
    
  #The target widget.
    
    ::EVB::inputStatistics::summaryDisplay .target
    pack .target
    
    # very simple controls.
    
    toplevel .controls
    label .controls.fragl -text Fragments
    entry .controls.frags -textvariable -fragments
    
    label .controls.oldestl -text Oldest
    entry .controls.oldest  -textvariable -oldest
    
    label .controls.newestl -text Newest
    entry .controls.newest  -textvariable -newest
    
    label .controls.deepestidl -text {deepest queue}
    entry .controls.deepestid  -textvariable -deepestid
    
    label .controls.deepestdl -text {deepest depth}
    entry .controls.deepestd  -textvariable -deepestdepth
    
    
    grid .controls.fragl      .controls.frags
    grid .controls.oldestl    .controls.oldest
    grid .controls.newestl    .controls.newest
    grid .controls.deepestidl .controls.deepestid
    grid .controls.deepestdl  .controls.deepestd
    
    # set traces on the control panel vars that update the
    # options in the control target.
    
    foreach var [list -fragments -oldest -newest -deepestid -deepestdepth] {
        trace add variable ::$var write [list ::EVB::test::updateWidgetOption .target]
    }
    
}

##
# ::EVB::test::inputStatistics::queueDisplay
#
#  test the queue display widget.
#
proc ::EVB::test::inputStatistics::queueDisplay {} {
    
    # Create the test panel.
    
    ::EVB::inputStatistics::queueDisplay .target
    pack .target
    
    # Create a control panel for its parameters.
    
    toplevel .control
    
    label .control.idl -text {queue id}
    entry .control.id  -textvariable -id
    
    label .control.depthl -text {depth}
    entry .control.depth  -textvariable -depth
    
    label .control.oldestl -text {oldest timestamp}
    entry .control.oldest  -textvariable -oldest
    
    label .control.outcountl -text {Output count}
    entry .control.outcount  -textvariable -outcount
    
    grid .control.idl .control.id
    grid .control.depthl .control.depth
    grid .control.oldestl .control.oldest
    grid .control.outcountl .control.outcount
    
    
    
    # Register variable traces
    
    foreach var [list -id -depth -oldest -outcount] {
        trace add variable ::$var write [list ::EVB::test::updateWidgetOption .target]
    }
        
    
}


##
# Testing utility to update a widget option from a trace on a variable
# that has a name matching the option:
#
# @param widget - the widget to modify.
# @param name1  - name of the variable (and option)
# @param name2  - index of the variable if an array.
# @param op     - operation performed.
#

if {[info proc ::EVB::test::updateWidgetOption] eq ""} {
    proc ::EVB::test::updateWidgetOption {widget name1 name2 op} {
        upvar #0 $name1 value
        $widget configure $name1 $value
        return ""
    }
}
